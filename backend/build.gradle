buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE")
    }
}

plugins {
    id 'java'
    id 'distribution'
    id 'checkstyle'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'ua.in.sz.english'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

checkstyle {
    toolVersion '7.8.1'
    configFile file("checkstyle.xml")
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = 'english'
    archiveVersion = '0.1.0'
    from('../frontend/dist/static') {
        into 'static'
    }
}

bootRun {
    group = APP_BACKEND_TASK_GROUP
}

test {
    useJUnitPlatform()
}

distributions {
    //noinspection GroovyAssignabilityCheck
    main {
        distributionBaseName = 'english'
        contents {
            from 'src/cmd'
            from bootJar
        }
    }
}

bootJar.dependsOn(checkstyleMain)
bootJar.dependsOn(':frontend:assemble')

task copy2bin(type: Copy) {
    group = "distribution"

    from file("$buildDir/install/english")
    into file("$rootDir/.bin")
}

copy2bin.dependsOn(installDist)

dependencyManagement {
    imports {
        mavenBom "io.projectreactor:reactor-bom:Aluminium-SR1"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-batch"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.2"

    // data processing
    implementation 'com.itextpdf:itextpdf:5.5.13.1'
    implementation 'org.apache.opennlp:opennlp-tools:1.9.1'

    // data store
    implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
    implementation "org.hsqldb:hsqldb"
    implementation 'org.apache.lucene:lucene-core:8.1.1'
    implementation 'org.apache.lucene:lucene-queryparser:8.1.1'

    // common utils
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'org.apache.commons:commons-text:1.7'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-io:commons-io:2.6'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.fusesource.jansi:jansi:1.8'

    // development
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // testing
    testImplementation 'com.tngtech.archunit:archunit-junit5-api:0.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.23.4'
    testRuntimeOnly 'com.tngtech.archunit:archunit-junit5-engine:0.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
}
