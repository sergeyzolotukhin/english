plugins {
    id 'java'
    id 'distribution'
    id 'antlr'
}

version 'unspecified'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ua.in.sz.english.dict2json.antlr.AntlrApplication'
        )
    }

    archiveFileName = 'dict2json.jar'

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

distributions {
    //noinspection GroovyAssignabilityCheck
    main {
        distributionBaseName = 'dict2json'
        contents {
            from 'src/cmd'
            from jar
        }
    }
}

task copy2bin(type: Copy) {
    group = "distribution"

    from file("$buildDir/install/dict2json")
    into file("$rootDir/.bin")
}

copy2bin.dependsOn(installDist)

dependencies {
    implementation 'commons-cli:commons-cli:1.4'

    // parsing
    antlr "org.antlr:antlr4:4.7.2"
    implementation "org.antlr:antlr4-runtime:4.7.2"

    // common utils
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'org.apache.commons:commons-text:1.7'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.6'

    // development
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    // testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.projectlombok:lombok:1.18.8'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.8'
}
